module pcf

signature
  constructors
    Var  : String -> Exp
    App  : Exp * Exp -> Exp
    Abs  : String * Type * Exp -> Exp
    Zero : Exp
    Succ : Exp -> Exp
    Pred : Exp -> Exp
    Ifz  : Exp * Exp * Exp -> Exp

    Closure : Exp * List(Val) -> Val
    Z       : Val
    S       : Val -> Val

    Num  : Type
    Fun  : Type * Type -> Type

    Nil  : List(a)
    Cons : a * List(a) -> List(a)
         : ()
         : a -> (a)
         : a * b -> (a, b)
: a * b * c -> (a, b, c)

strategies

  check: (ctx, Var(x)) -> <lookup> (x,ctx)
  check: (ctx, Abs(x,t,e)) -> <check> ([(x,t)|ctx], e)
  check: (ctx, App(e1,e2)) -> t2
    where <check> (ctx,e1) => Fun(t1,t2);
          <check> (ctx,e2) => t1
  check: (_, Zero()) -> Num()
  check: (ctx, Succ(e)) -> Num()
    where <check> (ctx,e) => Num()
  check: (ctx, Pred(e)) -> Num()
    where <check> (ctx,e) => Num()
  check: (ctx, Ifz(e1,e2,e3)) -> t
    where <check> (ctx,e1) => Num();
          <check> (ctx,e2) => t;
          <check> (ctx,e2) => t

  eval: (env, Var(x)) -> <lookup> (x,env)
  eval: (env, Abs(x,t,e)) -> Closure(Abs(x,t,e),env)
  eval: (env, App(e1,e2)) -> <eval> ([(x,arg)|env'],e)
    where <eval> (env,e1) => Closure(Abs(x,_,e),env');
          <eval> (env,e2) => arg
  eval: (_, Zero()) -> Z()
  eval: (env, Succ(e)) -> S(<eval>(env,e))
  eval: (env, Pred(e)) -> v
    where <eval> (env,e) => S(v)
  eval: (env, Ifz(e1,e2,e3)) -> <eval>(env,e2)
    where <eval> (env,e1) => Z()
  eval: (env, Ifz(e1,e2,e3)) -> <eval>(env,e3)
    where <eval> (env,e2) => S(_)

  check-eval: e -> <eval> ([], e)
    where <check> ([],e) => _

  lookup = (?(x,[(x,y)|_]); !y)
         <+ (?(x,[_|r]); <lookup> (x,r))